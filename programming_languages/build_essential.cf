#
#  Copyright (C) CFEngine AS
# 
#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU Lesser General Public License LGPL as published by the
#  Free Software Foundation; version 3.
#   
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  To the extent this program is licensed as part of the Enterprise
#  versions of CFEngine, the applicable Commerical Open Source License
#  (COSL) may apply to this file if you as a licensee so wish it. See
#  included file COSL.txt.
#
#######################################################
# C and C++ Programming environment
#######################################################
#
# CATEGORY:
#  Programing Languages
#
# LICENSE:
#  COPBL
#
# PLATFORMS:
#  Redhat, CentOS, Debian, Ubuntu, SUSE
#
# DESCRIPTION:
#  Install packages to compile C/C++ software from source
#
# REQUIREMENT:
#  CFEngine version 3.x.x
#  Public/Private software repository
#  inputs => {
#             "programming_languages/build_essential.cf", 
#             "cfengine_stdlib.cf",
#             };
# 
# USAGE:
#  In bundlesequence/methods, do like the following;
#
#   bundlesequence => { build_essential("$(state)") };
#
# or
#
#  methods:
#   "build-essential" usebundle => build_essential("$(state)");
#
#    $(state) = "on"    -> to install packages
#    $(state) = "purge" -> to uninstall packages
#
# CHANGES:
#  - added SUSE disto
#
# Author:
#  Nakarin Phooripoom
#
#
#

bundle agent build_essential(state)
{
 vars:

  redhat|centos|fedora::

   "package"   slist => { 
                         "gcc",
                         "gcc-c++",
                         "bison",
                         "flex",
                         "autoconf",
                         "automake",
                         "libtool",
                         "kernel-devel",
                        },
             comment => "List of build-essential packages for Redhat-related distributions",
              handle => "build_essential_vars_package_redhat_centos_fedora";

  ubuntu|debian::

   "package"   slist => {
                         "build-essential",
                        },
             comment => "List of build-essential packages for Debian-related distributions",
              handle => "build_essential_vars_package_debian_ubuntu";

  suse|SuSE::

   "package"   slist => {
                        "gcc",
                        "gcc-c++",
                        "bison",
                        "flex",
                        "autoconf",
                        "automake",
                        "libtool",
                        "kernel-devel",
                        },
             comment => "List of build-essential packages for SUSE linux",
              handle => "build_essential_vars_package_suse";

#

 classes:

  any::

   "on"    expression => strcmp("$(state)","on"),
              comment => "Check if to install packages",
               handle => "build_essential_classes_strcmp_on";
   "purge" expression => strcmp("$(state)","purge"),
              comment => "Check if to remove packages",
               handle => "build_essential_classes_strcmp_purge";

#

 packages:

  on::
 
   "$(package)"

             comment => "Install the C and C++ programming environment",
              handle => "build_essential_packages_add", 
      package_policy => "add",
      package_method => generic,
              action => log_repaired("stdout","C/C++ environment were installed");

  purge::

   "$(package)"

             comment => "Remove the C and C++ programming environment",
              handle => "build_essential_packages_remove", 
      package_policy => "delete",
      package_method => generic,
              action => log_repaired("stdout","C/C++ environment were removed");   
}
