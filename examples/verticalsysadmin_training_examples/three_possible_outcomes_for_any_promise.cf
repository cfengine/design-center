bundle common f { vars: "inputs" slist => findfiles("$(sys.masterdir)/lib/3.6/*.cf"); }
body common control

{
               inputs => { @(f.inputs) };
       bundlesequence => { "example" };
}

#######################################

bundle agent example

{
  files:

      "/etc"

         handle       => "safeguard_files_in_etc",
         comment      => "Keep screaming about changes in /etc",
         changes      => detect_all_change_noupdate,
         depth_search => recurse("inf"),
         classes      => kept_repaired_failed("promise_kept", "promise_repaired", "promise_not_kept");

  reports:
      promise_kept:: "Promise kept";
      promise_repaired:: "Promise repaired";
      promise_not_kept:: "Promise not kept";

}

body classes kept_repaired_failed(kept, repaired, failed)
{
     promise_kept     => { "$(kept)" };
     promise_repaired => { "$(repaired)" };
     repair_failed    => { "$(failed)" };
     repair_denied    => { "$(failed)" };
     repair_timeout   => { "$(failed)" };
}


body changes detect_all_change_noupdate

# This is fierce, and will cost disk cycles

{
       hash           => "best";
       report_changes => "all";
       update_hashes  => "no";
}

##

