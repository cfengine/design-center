body common control

{

       bundlesequence => { "update" };

}

#########################################################

bundle agent update
{
  vars:

      "master_location" string => "/var/cfengine/masterfiles";

  files:

      "/var/cfengine/inputs"

                perms => u_p("600"),
            copy_from => u_remote_cp("$(master_location)","205.186.156.208"),
         depth_search => u_recurse("inf"),
               action => u_immediate;

}

############################################

body perms u_p(p)

{
                mode  => "$(p)";
}


#########################################################

body copy_from u_remote_cp(from,server)
{
          servers     => { "$(server)" };
          source      => "$(from)";
          compare     => "mtime";
          trustkey    => "true";  # trust the server's public key
}

#########################################################

body depth_search u_recurse(d)

{
                depth => "$(d)";
                xdev  => "true";
}

#########################################################

body action u_immediate
{
            ifelapsed => "0";
}


# I had to add the client's IP address to the server's promises.cf: 
#
# diff -r1.1 /var/cfengine/inputs/promises.cf
# 103,105c103,105
# < allowconnects         => { "127.0.0.1" , "::1" };
# < allowallconnects      => { "127.0.0.1" , "::1" };
# < trustkeysfrom         => { "127.0.0.1" , "::1" };
# ---
# > allowconnects         => { "127.0.0.1" , "::1", "205.186.147.194" };
# > allowallconnects      => { "127.0.0.1" , "::1", "205.186.147.194" };
# > trustkeysfrom         => { "127.0.0.1" , "::1", "205.186.147.194" };

# and I had to make sure the server's site.cf promised access to the client for this directory: 
#
# 
# bundle server access_rules()
# {
# access:
# 
#  "/var/cfengine/masterfiles"
# 
#     admit   => { "205.186.147.194" };
# 
# 
# roles:
# 
#   ".*"  authorize => { "root" };
# }

# note: run cf-serverd with --verbose option to troubleshoot it
