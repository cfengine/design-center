body common control {
               inputs => { "/var/cfengine/inputs/libraries/cfengine_stdlib.cf" };
}

bundle agent example {

  files:
      "/etc/ssh/sshd_config"
               handle => "sshd_must_use_protocol_2_only",
            edit_line => sshd_uses_protocol_2_only,
              comment => "Make sure SSHD does not use protocols 1,2; make sure it only uses protocol 2, to increase security",
              classes => if_repaired("sshd_config_file_was_repaired"),
               action => logme("promise $(this.handle)");

  commands:
    sshd_config_file_was_repaired::
      "/etc/init.d/sshd reload"
               handle => "run_sshd_init_script_to_reload_sshd_to_pick_up_new_config",
               action => logme("promise $(this.handle)");

}


body action logme(x)
{
           log_string => "$(sys.date) $(x)";

             log_kept => "/tmp/private_keptlog.log";
         log_repaired => "/tmp/private_replog.log";
           log_failed => "/tmp/private_faillog.log";

}



bundle edit_line sshd_uses_protocol_2_only {
      delete_lines: "Protocol 1,2";
      insert_lines: "Protocol 2";
}
