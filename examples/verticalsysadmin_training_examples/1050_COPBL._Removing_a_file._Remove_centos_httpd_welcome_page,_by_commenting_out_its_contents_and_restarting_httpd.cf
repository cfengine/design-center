# welcome.conf is part of the Apache RPM
# to preserver package integrity, comment out this file's contents
# instead of deleting the file

body common control {

    inputs => { "/var/cfengine/inputs/cfengine_stdlib.cf" };

}


bundle agent example {

   vars:
     "mycomment" string => "Let's keep a low profile and not advertise what software we are running";
  

    files:

        "/etc/httpd/conf.d/welcome.conf"

            comment => "$(mycomment)",
            edit_line => comment_out_everything,
            classes => if_repaired("reload_httpd");

    commands:
      reload_httpd::
        "/etc/init.d/httpd reload";

}


bundle edit_line comment_out_everything {

  replace_patterns:

    "^([^#].*)"

      replace_with => comment("#$(example.mycomment)# ");

}

