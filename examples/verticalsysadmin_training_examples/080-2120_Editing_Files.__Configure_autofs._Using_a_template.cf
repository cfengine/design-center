# This file controls the contents of /etc/sysconfig/autofs
# using the template /templates/autofs.tmpl
#
# contents of /templates/autofs.tmpl:
# MASTER_MAP_NAME="auto.master"
# TIMEOUT=300
# BROWSE_MODE="yes"
# LOGGING="verbose"
# MAP_OBJECT_CLASS="automountMap"
# ENTRY_OBJECT_CLASS="automount"
# MAP_ATTRIBUTE="ou"
# ENTRY_ATTRIBUTE="cn"
# VALUE_ATTRIBUTE="automountInformation"
# USE_MISC_DEVICE="yes"
# SEARCH_BASE="ou=__SITE__,ou=Sites,dc=VerticalSysadmin,dc=com"
#
# and /etc/site which contains, for example, lax 

bundle agent example {

  vars:

      "site"
               string => readfile( "/etc/site" , "3" );

  files:
      "/etc/sysconfig/autofs"
            edit_line => create_our_autofs("$(site)"),
               create => "true";
                     }


#####################################################

bundle edit_line create_our_autofs(site) {

  delete_lines:
      ".*";

      # /templates/autofs.tmpl

  insert_lines:

      "/templates/autofs.tmpl"
          insert_type => "file";                # insert copy of template 

  replace_patterns:

      "__SITE__"

         replace_with =>  our_replace_body("$(site)");
                                         }

#####################################################

body replace_with our_replace_body(site) {

        replace_value => "$(site)";

                                         }
