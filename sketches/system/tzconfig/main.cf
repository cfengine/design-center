body file control
{
      namespace => "cfdc_tzconfig";
}

# timezone should be a string representing the timezone
# At least in the linux distros I checked this should be
# relative to /usr/share/zoneinfo

bundle agent set(runenv, metadata, timezone, zoneinfo)
{
  vars:
      "vars" slist => { "@(default:$(runenv).env_vars)" };
      "$(vars)" string => "$(default:$(runenv).$(vars))";

      "tz_file" string => "$(zoneinfo)/$(timezone)";

    !test::
      "relocate" string => "", policy => "free";
    test::
      "relocate" string => "/tmp", policy => "free";

  classes:
      "$(vars)" expression => "default:runenv_$(runenv)_$(vars)";
      "timezone_exists" expression => fileexists("$(tz_file)");

  methods:
    verbose::
      "metadata" usebundle => default:report_metadata($(this.bundle), $(metadata)),
      inherit => "true";

  files:
    timezone_exists.(ubuntu|debian|gentoo)::
      "$(relocate)/etc/timezone"
      handle        => "tzconfig_files_etc_timezone",
      edit_defaults => default:empty,
      create        => "true",
      edit_line     => default:insert_lines("$(timezone)"),
      perms         => cfdc_tzconfig:test_aware_mog("644", "root", "root"),
      comment       => "Debian and Gentoo based systems also use the /etc/timezone file";

    timezone_exists.(redhat|centos)::
      "$(relocate)/etc/sysconfig/clock"
      handle     => "tzconfig_files_etc_sysconfig_clock",
      create     => "true",
      edit_line  => default:replace_or_add("^ZONE=.*", "ZONE=\"$(timezone)\""),
      perms      => cfdc_tzconfig:test_aware_mog("644", "root", "root"),
      comment    => "Redhat like systems use /etc/sysconfig/clock for
the Time and Date Properties Tool
(system-config-date), and editing it does not
change the system timezone.";

    timezone_exists.linux::
      "$(relocate)/etc/localtime"
      handle            => "tzconfig_files_etc_localtime",
      copy_from         => cfdc_tzconfig:copy_zoneinfo("$(tz_file)"),
      classes           => default:if_repaired("tz_updated"),
      perms             => cfdc_tzconfig:test_aware_mog("644", "root", "root"),
      move_obstructions => "true",
      comment           => "Copy the proper timezone file in place. We dont use
a symlink because that might cause problems across
different filesystems, it has been a bug in the past.";

  reports:
    !timezone_exists::
      "Unable to set invalid timezone $(timezone): file $(tz_file) does not exist.";

      "" bundle_return_value_index => "timezone";

    timezone_exists::
      "$(timezone)" bundle_return_value_index => "timezone";

    tz_updated::
      "System timezone updated to $(timezone)";

}

body copy_from copy_zoneinfo(source)
{
      # Copy the file any symlinks reference this is superiour to symlinking
      # There have been bugs in the past that cause things to not function
      # properly if /etc/localtime is a symlink to a different filesystem
      source => "$(source)";
      copylink_patterns => { ".*", };
      compare => "hash";
}

body perms test_aware_mog(mode, user, group)
{
    !test::
      owners => { "$(user)" };
      groups => { "$(group)" };
      mode   => "$(mode)";
    test::
      owners => { getenv("LOGNAME", 100) };
      mode   => "$(mode)";
}
