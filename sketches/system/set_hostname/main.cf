body file control
{
      namespace => "cfdc_hostname";
}

bundle agent set(runenv, metadata, hostname, domainname)
{
      #@include "REPO/sketch_template/standard.inc"

  vars:

      # general config array
      "config[HOSTNAME]"  string => "$(hostname)";
      "config[NISDOMAIN]" string => "$(domainname)";

      # Stuff these into an array on gentoo style systems.  It's
      # convenient because of the way gentoo configs work
      "confdhostname[hostname]"  string => "$(hostname)";
      "confdnet[dns_domain_lo]" string => "$(domainname)";
      "confdnet[nis_domain_lo]" string => "$(domainname)";

    default:_stdlib_path_exists_domainname::
      "setdomain_command" string => "$(default:paths.path[domainname])";
    default:_stdlib_path_exists_hostname::
      "sethost_command" string => "$(default:paths.path[hostname])";

    gentoo::
      "sethost_command" string => "/bin/hostname";
      "setdomain_command" string => "/bin/domainname";

  files:
    debian::
      "$(path_prefix)/etc/hostname"
      comment => "Set host-name, not FQDN in this file - in accordance with man 1 hostname",
      handle => "cfdc_hostname_set_debian",
      create => "true",
      edit_defaults => default:empty,
      edit_line => default:insert_lines("$(config[HOSTNAME])"),
      perms => default:mog("644", "root", "root"),
      classes => default:if_repaired("set_hostname_update");

    redhat::
      "$(path_prefix)/etc/sysconfig/network"
      create => "true",
      handle => "cfdc_hostname_set_redhat",
      perms => default:mog("644", "root", "root"),
      edit_line => default:set_variable_values("cfdc_hostname:set.config"),
      classes => default:if_repaired("set_hostname_update"),
      comment => "While the official redhat docs state that you
should set HOSTNAME to the fqdn, it is apparently
incorrect. If you do this the hostname command
will return the fqdn instead of the shorthostname
like its supposed to when run without arguments.
I have been doing it wrong for years, and it only
recently bit me.";

    darwin::
      "$(path_prefix)/etc/defaultdomain"
      create => "true",
      edit_defaults => default:empty,
      perms => default:mog("644", "root", "root"),
      edit_line => default:insert_lines("$(domainname)"),
      classes => default:if_repaired("set_hostname_updatedomain"),
      handle => "cfdc_hostname_set_darwin";

    gentoo::
      "$(path_prefix)/etc/conf.d/hostname"
      create => "true",
      perms => default:mog("644", "root", "root"),
      edit_line => default:set_variable_values("cfdc_hostname:set.confdhostname"),
      classes => default:if_repaired("set_hostname_updatehostname"),
      handle => "cfdc_hostname_set_domainname_gentoo";

      "$(path_prefix)/etc/conf.d/net"
      create => "true",
      perms => default:mog("644", "root", "root"),
      edit_line => default:set_variable_values("cfdc_hostname:set.confdnet"),
      classes => default:if_repaired("set_hostname_updatedomain"),
      handle => "cfdc_hostname_set_hostname_gentoo";

  commands:
    darwin::
      "$(exec_prefix)/usr/sbin/scutil --set HostName $(hostname)"
      classes => default:if_repaired("set_hostname_updatehostname"),
      handle => "cfdc_hostname_set_scutil_darwin";

    redhat||darwin||gentoo||debian::
      "$(exec_prefix)$(sethost_command) $(hostname)"
      ifvarclass => "set_hostname_update||set_hostname_updatehostname",
      comment => "Update the hostname on the running system so we
dont have to wait for a reboot",
      handle => "cfdc_hostname_set_hostname_command";

    redhat||darwin||gentoo::
      "$(exec_prefix)$(setdomain_command) $(domainname)"
      ifvarclass => "set_hostname_update||set_hostname_updatedomain",
      comment => "Update the domainname on the running system so
we dont have to reboot for it",
      handle => "cfdc_hostname_set_domainname_command";

  reports:
    any::
      "$(hostname)" bundle_return_value_index => "hostname";
      "$(domainname)" bundle_return_value_index => "domainname";

    verbose::
      "$(dcbundle): setting hostname=$(hostname), domainname=$(domainname)";
      "$(dcbundle): hostname command = $(sethost_command)";
      "$(dcbundle): domainname command = $(setdomain_command)";

    !debian.!redhat.!darwin.!gentoo::
      "$(dcbundle): Not on RedHat or Darwin or Gentoo or Debian, nothing was done!  Please add support for your system!";
}
