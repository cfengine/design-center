body file control
{
      namespace => "cfdc_hostname";
}

bundle agent set(canon_prefix, hostname, domainname)
{
  meta:
      "vars[canon_prefix][type]"         string => "NON_EMPTY_STRING";
      "vars[canon_prefix][default]"      string => "__CANON_PREFIX__";

      "vars[hostname][type]"             string => "NON_EMPTY_STRING";
      "vars[domainname][type]"           string => "NON_EMPTY_STRING";

  vars:
    redhat::
      # Stuff these into an array on redhat style systems.  It's
      # convenient because of the way redhat configs work
      "config[HOSTNAME]"  string => "$(hostname)";
      "config[NISDOMAIN]" string => "$(domainname)";

      "sethost_command" string => "/bin/hostname";
      "setdomain_command" string => "/bin/domainname";

    debian::
      "sethost_command" string => "/bin/hostname";

    darwin::
      "sethost_command" string => "/bin/hostname";
      "setdomain_command" string => "/bin/domainname";

    gentoo::
      # Stuff these into an array on gentoo style systems.  It's
      # convenient because of the way gentoo configs work
      "confdhostname[hostname]"  string => "$(hostname)";
      "confdnet[dns_domain_lo]" string => "$(domainname)";
      "confdnet[nis_domain_lo]" string => "$(domainname)";

      "sethost_command" string => "/bin/hostname";
      "setdomain_command" string => "/bin/domainname";

  files:
    debian::
      "/etc/hostname"
      comment       => "Set domain-name, not FQDN in this file - in accordance with man 1 hostname",
      create        => "true",
      edit_defaults => default:empty,
      edit_line     => default:insert_lines("$(config[HOSTNAME])"),
      perms         => default:mog("644", "root", "root"),
      classes       => default:if_repaired("$(canon_prefix)_update");

    redhat::
      "/etc/sysconfig/network"
      create    => "true",
      perms     => default:mog("644", "root", "root"),
      edit_line => default:set_variable_values("cfdc_hostname:set.config"),
      classes   => default:if_repaired("$(canon_prefix)update"),
      comment   => "While the official redhat docs state that you
should set HOSTNAME to the fqdn, it is apparently
incorrect. If you do this the hostname command
will return the fqdn instead of the shorthostname
like its supposed to when run without arguments.
I have been doing it wrong for years, and it only
recently bit me.";

    darwin::
      "/etc/defaultdomain"
      create    => "true",
      edit_defaults => default:empty,
      perms     => default:mog("644", "root", "root"),
      edit_line => default:insert_lines("$(domainname)"),
      classes   => default:if_repaired("$(canon_prefix)updatedomain");

    gentoo::
      "/etc/conf.d/hostname"
      create	=> "true",
      perms	=> default:mog("644", "root", "root"),
      edit_line	=> default:set_variable_values("cfdc_hostname:set.confdhostname"),
      classes	=> default:if_repaired("$(canon_prefix)updatehostname");

      "/etc/conf.d/net"
      create	=> "true",
      perms	=> default:mog("644", "root", "root"),
      edit_line	=> default:set_variable_values("cfdc_hostname:set.confdnet"),
      classes	=> default:if_repaired("$(canon_prefix)updatedomain");

  commands:
    darwin::
      "/usr/sbin/scutil --set HostName $(hostname)"
      classes => default:if_repaired("$(canon_prefix)updatehostname");

    redhat||darwin||gentoo||debian::
      "$(sethost_command) $(hostname)"
      ifvarclass => "$(canon_prefix)update||$(canon_prefix)updatehostname",
      comment => "Update the hostname on the running system so we
dont have to wait for a reboot";

    redhat||darwin||gentoo::
      "$(setdomain_command) $(domain)"
      ifvarclass => "$(canon_prefix)update||$(canon_prefix)updatedomain",
      comment => "Update the domainname on the running system so
we dont have to reboot for it";

  reports:
    !debian.!redhat.!darwin.!gentoo::
      "Not on RedHat or Darwin or Gentoo or Debian, nothing was done!  Please add support for your system!";
}
