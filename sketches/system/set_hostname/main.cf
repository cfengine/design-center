bundle agent cfdc_set_hostname(prefix) {
# Expects desired shorthostname and domain name both as strings;
# Usage:
#   vars:
#       "fqdn_hostname" string => "jim";
#       "fqdn_domain"   string => "bob.com";
#
#   methods:
#       "hostname" usebundle => cfdc_set_hostname("main.fqdn_");
#
    vars:
        "config[HOSTNAME]"  string => "$($(prefix)hostname)";
        "config[NISDOMAIN]" string => "$($(prefix)domain)";

    files:

        debian::
            "/etc/hostname"
                comment       => "Set domain-name, not FQDN in this file - in accordance with man 1 hostname",
                create        => "true",
                edit_defaults => empty,
                edit_line     => insert_lines("$(config[HOSTNAME])"),
                perms         => mog("644", "root", "root"),
                classes       => if_repaired("$(canon_prefix)_set_hostname_update_running_hostname");


        redhat::
            "/etc/sysconfig/network"
                create    => "true",
                perms     => mog("644", "root", "root"),
                edit_line => set_variable_values("cfdc_set_hostname.config"),
                classes   => if_repaired("$(canon_prefix)_set_hostname_update_running_hostname"),
                comment   => "While the official redhat docs state that you
                              should set HOSTNAME to the fqdn, it is apparently
                              incorrect. If you do this the hostname command
                              will return the fqdn instead of the shorthostname
                              like its supposed to when run without arguments.
                              I have been doing it wrong for years, and it only
                              recently bit me.";

    commands:

        debian|redhat::
            "/bin/hostname $($(prefix)hostname)"
                ifvarclass => "$(canon_prefix)_set_hostname_update_running_hostname",
                comment => "Update the hostname on the running system so we
                            dont have to wait for a reboot";

            "/bin/domainname $($(prefix)domain)"
                ifvarclass => "$(canon_prefix)_set_hostname_update_running_hostname",
                comment => "Update the domainname on the running system so
                            we dont have to reboot for it";

}

bundle agent meta_cfdc_set_hostname{
vars:
    "argument[hostname]" string => "string";
    "argument[domain]"   string => "string";
}
