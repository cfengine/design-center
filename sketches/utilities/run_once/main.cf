body file control
{
      namespace => "cfdc_utilities_run_once";
}

bundle agent run(runenv, metadata, context, scripts)
{
#@include "REPO/sketch_template/standard.inc"
  vars:
      "a2string" string => format("%S %s", scripts, $(context));
      "activation_id" string => hash($(a2string), "sha1");

  methods:
      "checkit" usebundle => checkit($(scripts),
                                     $(activation_id),
                                     "$(scripts) $(activation_id)"),
      inherit => "true";

      "runit" usebundle => runit($(scripts),
                                 $(context),
                                 $(activation_id),
                                 "$(scripts) $(activation_id)"),
      inherit => "true";

  reports:
    dc_verbose::
      "$(dcbundle): The unique ID is $(activation_id)";
}

bundle agent checkit(script, id, pname)
{
  classes:
      "$(pc)_unknown" not => $(pc),
      scope => "namespace";

  vars:
      "pc" string => canonify($(pname));

  reports:
    dc_verbose::
      "$(dcbundle): Checking if $(pname) is defined";

      "$(dcbundle): $(pc) is defined" ifvarclass => $(pc);
      "$(dcbundle): $(pc) is NOT defined" ifvarclass => "!$(pc)";

      "$(dcbundle): $(pc)_unknown is defined" ifvarclass => "$(pc)_unknown";
      "$(dcbundle): $(pc)_unknown is NOT defined" ifvarclass => "!$(pc)_unknown";

}

bundle agent runit(cmd, context, id, pname)
{
  vars:
      "pc" string => canonify($(pname));

    have_file::
      "contexts" slist => readstringlist($(context), "#.*", "\s", 1000, 40000);
    !have_file::
      "contexts" slist => { $(context) };

  classes:
      "have_file" expression => fileexists($(context_or_file));

      "contexts_found" expression => classify($(contexts));

  commands:
      "$(cmd)"
      contain => default:in_shell,
      comment => "Run the given command iff the context is true and we haven't run it before.",
      ifvarclass => concat(canonify($(pname)), "_unknown.contexts_found"),
      classes => run_once_persist($(pname));

  reports:
    dc_verbose::
      "$(dcbundle): WE HAVE the required context '$(context)'"
      ifvarclass => "$(context)";

      "$(dcbundle): WE LACK the required context '$(context)'"
      ifvarclass => "!($(context))";

      "$(dcbundle): The command ran or has already run for context '$(pc)'"
      ifvarclass => $(pc);

      "$(dcbundle): The command did not run successfully and has not already run for context '$(pc)'"
      ifvarclass => "$(pc)_unknown";
}

body classes run_once_persist(x)
{
      persist_time => "90m";
      promise_repaired => { $(x) };
      promise_kept => { $(x) };
}
